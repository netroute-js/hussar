plugins {
    id "java-library"
    id "io.freefair.lombok"
    id "hussar.artifact-metadata-conventions"
}

java {
    sourceCompatibility = "21"

    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

sourceSets {

    integrationTest {
        java.srcDir file("src/integration-test/java")
        resources.srcDir file("src/integration-test/resources")

        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }

}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestCompileOnly.extendsFrom testCompileOnly
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen {false}
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = "full"
    useJUnitPlatform()
}

integrationTest.dependsOn test
check.dependsOn integrationTest

repositories {
    mavenCentral()
}