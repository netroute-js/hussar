sourceSets {

	integrationTest {
		java.srcDir file('src/integration-test/java')
		resources.srcDir file('src/integration-test/resources')

		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}

}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntime.extendsFrom testRuntime
	integrationTestCompileOnly.extendsFrom compileOnly
}

task integrationTest(type: Test) {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen {false}
	testLogging.showStandardStreams = true
	testLogging.exceptionFormat = 'full'
	useJUnitPlatform()
}

integrationTest.dependsOn test
check.dependsOn integrationTest

dependencies {
	implementation project(":hussar-core")
	implementation project(":hussar-services:hussar-core-service")
	implementation libs.slf4j.api
	implementation libs.testcontainers

	testImplementation libs.logback.classic
	testImplementation libs.bundles.testing
	testImplementation libs.jedis.connector
	testImplementation testFixtures(project(":hussar-services:hussar-core-service"))
}